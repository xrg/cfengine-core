body classes if_failed(x)
{
      repair_failed => { "$(x)" };
}

body contain run_under_shell
{
      useshell => "useshell";
}


bundle agent generate_key
{
  commands:
      "$(sys.cf_key)";
}


bundle agent start_server(server_name)
{
  vars:
      "cwd" string => dirname("$(this.promise_filename)");
      "dlog" string => "$(sys.workdir)/server-debug.$(server_name).log";
      "conf" string => "$(cwd)/$(server_name).srv";

  commands:
    !debug_mode::
      "$(sys.cf_serverd) -Kf $(conf)"
      classes => if_failed("server_failed");

    debug_mode::
      "$(sys.cf_serverd) -Kdf $(conf) > $(dlog) 2>&1 &"
      contain => run_under_shell,
      handle => "run_debug_server",
      classes => if_failed("server_failed");

      # Sleep 3 seconds since cf-serverd takes some time to start in debug mode.
      "sleep 3"
      depends_on => { "run_debug_server" },
      contain => run_under_shell;

  reports:
    debug_mode::
      "$(sys.cf_serverd)($(server_name)) was run in debug mode with configuration $(conf).  The logs will be in $(dlog)";
}

bundle agent run_test(test_name)
{
  commands:
      "$(sys.cf_agent) -Kf $(test_name) -D DEBUG,AUTO,ok";
}


bundle agent stop_server(server_name)
{
  processes:
      "$(server_name)"
      signals => { "term", "kill" };
}
